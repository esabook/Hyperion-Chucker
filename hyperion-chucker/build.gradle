apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 32
        versionCode findProperty("build.versionCode") as Integer
        versionName findProperty("build.versionName")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    resourcePrefix 'hchuck_'

}

dependencies {
    api 'androidx.annotation:annotation:1.4.0'
    api 'com.willowtreeapps.hyperion:hyperion-plugin:0.9.34'
    api 'io.github.esabook.chucker:library:4.0.2'
    annotationProcessor "com.google.auto.service:auto-service:1.0.1"
}

//apply from: 'https://raw.githubusercontent.com/Commit451/gradle-android-javadocs/1.0.0/gradle-android-javadocs.gradle'


publishing {
    repositories {
        maven {
            name "snapshot"
            url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username = findProperty("NEXUS_USERNAME")
                password = findProperty("NEXUS_PASSWORD")
            }
        }
        maven {
            name "staging"
            url = "https://s01.oss.sonatype.org/content/groups/staging"
            credentials {
                username = findProperty("NEXUS_USERNAME")
                password = findProperty("NEXUS_PASSWORD")
            }
        }
    }

    publications {
        release(MavenPublication) {
            pom {
                name = 'Hyperion-Chucker'
                description = 'Chucker implementation as Hyperion plugin '
                url = 'http://github.com/esabook/hyperion-chucher'
                licenses {
                    license {
                        name = 'MIT license'
                        url = 'https://github.com/esabook/Hyperion-Chucker/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'egit'
                        name = 'Egit'
                        email = 'esa_book@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/esabook/Hyperion-Chucker.git'
                    developerConnection = 'scm:git:ssh://github.com/esabook/Hyperion-Chucker.git'
                    url = 'http://github.com/esabook/Hyperion-Chucker'
                }
            }
        }
    }
}



// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'io.github.esabook'
                artifactId = 'hyperion-chucker'
                version = findProperty("build.versionName")
            }
        }
    }
}


signing {
    sign publishing.publications.release
}